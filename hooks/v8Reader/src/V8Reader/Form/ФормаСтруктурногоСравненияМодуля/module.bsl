
//==============================================================================================================================================
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
//==============================================================================================================================================

Процедура ПриОткрытии()
	
	Заголовок = Заголовок + " <" + Модуль + ">";;
	
	ТекстПервогоМодуля = Новый ТекстовыйДокумент;
	ТекстПервогоМодуля.Прочитать(ПервыйМодуль);
	ВремТабПервогоМодуля = ПолучитьТаблицуПроцедурМодуля(ТекстПервогоМодуля.ПолучитьТекст()).Скопировать();
	
	ТекстВторогоМодуля = Новый ТекстовыйДокумент;
	ТекстВторогоМодуля.Прочитать(ВторойМодуль);
	ВремТабВторогоМодуля = ПолучитьТаблицуПроцедурМодуля(ТекстВторогоМодуля.ПолучитьТекст()).Скопировать();
	
	ПолучитьТаблицуСтруктурногоСравнения(ВремТабПервогоМодуля, ВремТабВторогоМодуля, СтруктурноеСравнениеМодуля, ТолькоИзмененные)
	
КонецПроцедуры

//==============================================================================================================================================
// ОБРАБОТЧИКИ СОБЫТИЙ КНОПОК ФОРМЫ
//==============================================================================================================================================

Процедура ВывестиПростоеСравнениеМодулей(Элемент)
	Если ИспользоватьВнешнююПрограммуСравнения Тогда
		СтрокаЗапуска = "" + ВыборDiff.ПутьПрограммы + " " + ПервыйМодуль + " " + ВторойМодуль;
		Попытка
			ЗапуститьПриложение(СтрокаЗапуска);
		Исключение
		КонецПопытки;
	Иначе
		СравнениеФайлов = Новый СравнениеФайлов;
		СравнениеФайлов.ПервыйФайл = ПервыйМодуль;
		СравнениеФайлов.ВторойФайл = ВторойМодуль;
		СравнениеФайлов.ИгнорироватьПустоеПространство = Истина;
		СравнениеФайлов.УчитыватьРегистр = Истина;
		СравнениеФайлов.СпособСравнения = СпособСравненияФайлов.ТекстовыйДокумент;
		СравнениеФайлов.ПоказатьРазличия();
	КонецЕсли;
КонецПроцедуры

//==============================================================================================================================================
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ
//==============================================================================================================================================

Процедура СтруктурноеСравнениеМодуляВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	СравнениеПроцедур = ТекстовоеСравнениеВоВременныхФайлах(ВыбраннаяСтрока.ТекстПервогоЭлемента, ВыбраннаяСтрока.ТекстВторогоЭлемента, ВыбраннаяСтрока.СтруктураПервогоМодуля, ВыбраннаяСтрока.СтруктураВторогоМодуля);
	Если ИспользоватьВнешнююПрограммуСравнения Тогда
		СтрокаЗапуска = "" + ВыборDiff.ПутьПрограммы + " " + СравнениеПроцедур.ПервыйФайл + " " + СравнениеПроцедур.ВторойФайл;
		Попытка
			ЗапуститьПриложение(СтрокаЗапуска);
		Исключение
		КонецПопытки;
	Иначе
		СравнениеПроцедур.ПоказатьРазличия();
	КонецЕсли;
КонецПроцедуры

Процедура СтруктурноеСравнениеМодуляПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	
	Если ДанныеСтроки.ЕстьИзменения = 3 Тогда
		ОформлениеСтроки.Ячейки.СтруктураПервогоМодуля.ОтображатьКартинку = Ложь;
		ОформлениеСтроки.ЦветФона = Новый Цвет(192, 220, 192);
	ИначеЕсли ДанныеСтроки.ЕстьИзменения = 2 Тогда
		ОформлениеСтроки.Ячейки.СтруктураВторогоМодуля.ОтображатьКартинку = Ложь;
		ОформлениеСтроки.ЦветФона = Новый Цвет(255, 228, 196);
	Иначе
		Если ДанныеСтроки.ЕстьИзменения = 1 Тогда
			ОформлениеСтроки.ЦветФона = Новый Цвет(225, 253, 253);
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеСтроки.Сортировка = "_РазделПеременных_" ИЛИ ДанныеСтроки.Сортировка = "_РазделОператоров_" Тогда
		ОформлениеСтроки.Ячейки.СтруктураПервогоМодуля.ОтображатьКартинку = Ложь;
		ОформлениеСтроки.Ячейки.СтруктураВторогоМодуля.ОтображатьКартинку = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ТолькоИзмененныеПриИзменении(Элемент)
	
	КодВозврата = Вопрос("Будет переформирована таблица структурного сравнения модуля. Продолжить?", РежимДиалогаВопрос.ОКОтмена);
	Если КодВозврата = КодВозвратаДиалога.ОК Тогда
		СтруктурноеСравнениеМодуля.Очистить();
		ПриОткрытии();
	Иначе
		ТолькоИзмененные = НЕ ТолькоИзмененные;
	КонецЕсли;

КонецПроцедуры

//==============================================================================================================================================
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
//==============================================================================================================================================

